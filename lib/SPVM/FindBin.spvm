# Copyright (c) 2023 Yuki Kimoto
# MIT License

class FindBin {
  
  version "0.031";
  
  use Sys;
  use Cwd;
  use File::Spec;
  use File::Basename;
  
  our $Bin : ro string;
  our $Script : ro string;
  our $RealBin : ro string;
  our $RealScript : ro string;
  
  INIT {
    &init();
  }
  
  static method init : void () {
    
    my $script = CommandInfo->PROGRAM_NAME;
    
    unless (Sys->f($script)) {
      die "Cannot find current script \"$script\"";
    }
    
    unless (File::Spec->file_name_is_absolute($script)) {
      $script = File::Spec->catfile([Cwd->getcwd], $script);
    }
    
    my $fileparse_ret = File::Basename->fileparse($script);
    $Script = $fileparse_ret->[0];
    $Bin = $fileparse_ret->[1];
    
    while (1) {
      
      my $fileparse_ret = File::Basename->fileparse($script);
      $RealScript = $fileparse_ret->[0];
      $RealBin = $fileparse_ret->[1];
      
      unless (Sys->l($script)) {
        last;
      }
      
      my $link_text = Sys->readlink($script);
      
      unless ($link_text) {
        last;
      }
      
      if (File::Spec->file_name_is_absolute($link_text)) {
        $script = $link_text;
      }
      else {
        $script = File::Spec->catfile([$RealBin], $link_text);
      }
    }
    
    $Bin = Cwd->abs_path($Bin);
    
    $RealBin = Cwd->abs_path($RealBin);
  }

  static method again : void () {
    &init();
  }
}
